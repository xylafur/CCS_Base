/*******************************************************************/ 
/* This GEL file is loaded on the command line of Code Composer    */
/* The StartUp() function is called every time you start           */
/* Code Composer.  You can customize this function to              */
/* initialize wait states or to perform other initialization.      */
/*                                                                 */
/* Author: Salamito thomas                               		   */
/*******************************************************************/

menuitem "PHOENIX"

/* ============================================================================
* GLOBAL VARIABLES
* =============================================================================
*/
int Is_PALMASI2C_Initialized = 0;

/* ============================================================================
* LOCAL TYPES
* =============================================================================
*/

#define INFO_PRINT								1

#define I2C_BASEADDRESS  	 		0x48070000
#define I2C_INSTANCE		 		0x14
#define PALMAS_I2C_FAST_SPEED_OP 		0x1
#define MSI2C_FULL_SPEED_MODE 		0x0
#define MSI2C_NO_START_BYTE_MODE 	0x0
#define MSI2C_7BIT_ADDRESS_MODE 	0x0
#define MSI2C_MASTERCODE 			0x5
#define PALMASI2C_INTH_NOT_INSTALLED	0x0
#define PALMAS_I2C_SLAVE_ADDRESS		0x48


/* ============================================================================
* FUNCTIONS
* =============================================================================
*/

hotmenu omap5430_PALMAS_Init()
{

	if (INFO_PRINT) {  
	GEL_TextOut("Start_Omap5430_PALMAS_Init\n",,2);
	}
	
	PALMAS_I2C_Init(I2C_BASEADDRESS,I2C_INSTANCE,0x19,PALMAS_I2C_FAST_SPEED_OP);
	
	if (INFO_PRINT) {	
	GEL_TextOut("End_Omap5430_PALMAS_Init\n",,2);
	}
}


/* --------------------------------------------------------------
	Function Name:	PALMAS_I2C_Init
	Description:	Init I2C Link
	Parameters: 
 ------------------------------------------------------------- */
PALMAS_I2C_Init( int  I2C_baseAddress, int I2C_module, int I2C_ownAddress, int operationI2CMode)
{
	if (INFO_PRINT) {
	GEL_TextOut("PALMAS_I2C_Init Function\n",,1);
	}

	Is_PALMASI2C_Initialized = 1;
		
	/* Enable I2C Clocks */
	PRCMLIB_EnableModule();
	
    /* Init I2C */
	MSI2C_Init(			I2C_baseAddress,
                        3,    /* prescaler     = 3 + 1 = 1 -> ICLK = 24 MHz  */
                        24,   /* scll period   = 24 + 7 = 31 * ICLKPeriod */
                        24,   /* sclh period   = 24 + 5 = 29 * ICLKPeriod */
                        8,    /* hsscll period = 8 + 7 = 15  * ICLKPeriod => DONT CARE */
                        9     /* hssclh period = 9 + 5 = 14  * ICLKPeriod => DONT CARE */
              );
    
    /* Configure MSI2C module */
	MSI2C_Configure	( 	I2C_baseAddress,
						MSI2C_FULL_SPEED_MODE,
						MSI2C_NO_START_BYTE_MODE,
						MSI2C_7BIT_ADDRESS_MODE,
						I2C_ownAddress,
						MSI2C_MASTERCODE
					);
	if (INFO_PRINT) {
	GEL_TextOut("PALMAS_I2C_Init Done\n",,2);
	}
}

/* --------------------------------------------------------------
	Function Name:	PALMAS_I2C_Read
	Description:	Read PALMAS Register
	Parameters:
 ------------------------------------------------------------- */
PALMAS_I2C_Read8( short slaveAddress, char RegAddress, char pReceivedData )
{
	short data = 0x0;
	if (INFO_PRINT) {
	GEL_TextOut("PALMAS_I2C_Read8 Function\n",,1);
	}

	if(Is_PALMASI2C_Initialized==0)
	{
		PALMAS_I2C_Init(I2C_BASEADDRESS,I2C_INSTANCE,0x19,PALMAS_I2C_FAST_SPEED_OP);
	}
	
	data = PALMAS_I2C_Read	(	I2C_BASEADDRESS, 			/* processor base address */
               				I2C_INSTANCE,    			/* instance number */
							slaveAddress,    			/* slave address */  
							RegAddress,      			/* reg address */   
							1,               			/* data number */ 
							pReceivedData,				/* Not Used in GEL Driver */
							PALMASI2C_INTH_NOT_INSTALLED	/* int status */
						);
	if (INFO_PRINT) {						
	GEL_TextOut("I2C Read Done [Read %x at Slave@ %x Reg@ %x]\n",,2,,,(char)data,(char)slaveAddress,(char)RegAddress);				
}
	return data;
}

/* --------------------------------------------------------------
	Function Name:	PALMAS_I2C_Write
	Description:	Write PALMAS Register
	Parameters: 
 ------------------------------------------------------------- */
PALMAS_I2C_Write8( short slaveAddress, short RegAddress, char SentData )
{		
	if (INFO_PRINT) {
	GEL_TextOut("PALMAS_I2C_Write8 Function\n",,1);
	}

	if(Is_PALMASI2C_Initialized==0)
	{
		PALMAS_I2C_Init(I2C_BASEADDRESS,I2C_INSTANCE,0x20,PALMAS_I2C_FAST_SPEED_OP);
	}
	
	PALMAS_I2C_Write(	I2C_BASEADDRESS,            /* processor base address */
                    I2C_INSTANCE,               /* instance number */ 
                    slaveAddress,               /* slave address */       
                    RegAddress,                 /* reg address */  
                    1,                 			/* data number */  
                    SentData,
                    PALMASI2C_INTH_NOT_INSTALLED    /* int status */
				);

    if (INFO_PRINT) {
	GEL_TextOut("I2C Write Done [Write %x at Slave@ %x Reg@ %x]\n",,2,,,(char)SentData,(char)slaveAddress,(char)RegAddress);				
	}
}

/* --------------------------------------------------------------
	Function Name:	PHALMAS_I2C_Read
	Description:	Read PALMAS Register
	Parameters: 	
 ------------------------------------------------------------- */
PALMAS_I2C_Read( 	int I2C_baseAddress,
				char I2C_instNum,
				short slaveAddress,
				char RegAddress,
				short dataNumber,
				char pReceivedData,
				char inthStatus )
{
	short data = 0x0;
    
    int returnValue =0x0;
	
	if (INFO_PRINT) {
	GEL_TextOut("PALMAS_I2C_Read Function\n",,1);
	}

	if(inthStatus == PALMASI2C_INTH_NOT_INSTALLED)
	{
		returnValue = MSI2C_MasterSendPolling( 	I2C_baseAddress,
                             		slaveAddress,
									RegAddress,
                             		0x0, /* no data to send */
                             		1);

        if (returnValue == 0x0)
        {

		    data = MSI2C_MasterReceivePolling(	I2C_baseAddress,       
                                			slaveAddress,   
                                			pReceivedData, /* Not use in GEL Driver */
                                			dataNumber);
         }                                   
	}

	return data;
}

/* --------------------------------------------------------------
	Function Name:	PALMAS_I2C_Write
	Description:	Write PALMAS Register
	Parameters: 	
 ------------------------------------------------------------- */
PALMAS_I2C_Write(	int	I2C_baseAddress,
          		char I2C_instNum,
          		short slaveAddress,
         		char RegAddress,
          		short dataNumber,
       			char pSentData,
        		char inthStatus )
{

if (INFO_PRINT) {
	GEL_TextOut("PALMAS_I2C_Write Function\n",,1);
	

	GEL_TextOut("I2C_baseAddress %x\n",,,,,I2C_baseAddress);
	GEL_TextOut("I2C_instNum %x\n",,,,,RegAddress);
	GEL_TextOut("slaveAddress %x\n",,,,,slaveAddress);
	GEL_TextOut("RegAddress %x\n",,,,,RegAddress);
	GEL_TextOut("dataNumber %x\n",,,,,dataNumber);
	GEL_TextOut("pSentData %x\n",,,,,pSentData);
	GEL_TextOut("inthStatus %x\n",,,,,inthStatus);
}
	if(inthStatus == PALMASI2C_INTH_NOT_INSTALLED)
	{
		MSI2C_MasterSendPolling( 	I2C_baseAddress,
                             		slaveAddress,
									RegAddress,
                             		pSentData,
                             		dataNumber+1);
	}
}

/* --------------------------------------------------------------
	Function Name:	PRCMLIB_EnableModule
	Description:	Enable I2C2 Clocks
	Parameters: 	
 ------------------------------------------------------------- */
PRCMLIB_EnableModule()
{
	if (INFO_PRINT) {
	GEL_TextOut("Start_PRCMLIB_EnableModule Function\n",,1);
	}

	// Enable I2C1 
	*(int*)0x4A0094A0 |= 0x2;
	
	/* Wait I2C1 is accessible */
	while( ( (*(int*)(0x4A0094A0)) & 0x30000 ) != 0x0);
	
	if (INFO_PRINT) {
	GEL_TextOut("End_PRCMLIB_EnableModule Function\n",,1);
	}
}
